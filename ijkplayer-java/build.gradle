apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    // http://tools.android.com/tech-docs/new-build-system/tips
    //noinspection GroovyAssignabilityCheck
    compileSdkVersion rootProject.ext.compileSdkVersion
    //noinspection GroovyAssignabilityCheck
    buildToolsVersion rootProject.ext.buildToolsVersion

    lintOptions {
        abortOnError false
    }
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion rootProject.ext.targetSdkVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    lint {
        disable 'ExpiredTargetSdkVersion'
        checkReleaseBuilds false  // 可选：完全禁用release构建时的lint检查
        abortOnError false        // 可选：即使有错误也不中断构建
    }

    namespace 'tv.danmaku.ijk.media.player'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                group = PROJ_GROUP
                artifactId = PROJ_ARTIFACTID
                version = PROJ_VERSION


                pom {
                    name = PROJ_NAME
                    description = PROJ_DESCRIPTION
                    url = PROJ_VCSURL
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = DEVELOPER_ID
                            name = DEVELOPER_NAME
                        }
                    }
                    scm {
                        connection = PROJ_ISSUETRACKERURL
                        developerConnection = PROJ_VCSURL
                        url = PROJ_WEBSITEURL
                    }
                }
            }
        }
    }
}


publishing {
    repositories {
        maven {
            name = "GSYIjkJava"
            url = "https://maven.pkg.github.com/CarGuo/GSYVideoPlayer"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}




//apply from: new File(rootProject.projectDir, "tools/gradle-on-demand.gradle");
